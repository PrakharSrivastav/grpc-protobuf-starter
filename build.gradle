ext { grpcVersion = '1.13.1' }

group = "no.sysco.middleware.workshops"
version = "1.0-SNAPSHOT"

buildscript {
  // repositories { mavenCentral() }
  // The google mirror is less flaky than mavenCentral()
  repositories { maven { url "https://maven-central.storage-download.googleapis.com/repos/central/data/" } }
  dependencies { classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6' }
}
apply plugin: 'java'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
  main {
    java {}
    proto {}
  }
}

// Project dependencies
repositories { jcenter() }
dependencies {
  testCompile 'junit:junit:4.12'
  compile "com.google.api.grpc:proto-google-common-protos:1.0.0"
  compile "io.grpc:grpc-protobuf:${grpcVersion}"
  compile "io.grpc:grpc-stub:${grpcVersion}"
  compile "io.grpc:grpc-netty:${grpcVersion}"
  compile "ch.qos.logback:logback-classic:1.2.3"
}

// Configure protobuf tasks
protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.4.0'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  // directory where the generated file will be created
  generatedFilesBaseDir = "${projectDir}/src/"
  generateProtoTasks {
    all().each { t ->
      t.plugins {
        // subdirectory under generatedFilesBaseDir path where files should be generated
        grpc { outputSubDir = 'java' }
      }
    }
  }
}

// task to run server
task(runServer, dependsOn: 'classes', type: JavaExec) {
  main = 'no.sysco.middleware.workshops.impl.Server'
  classpath = sourceSets.main.runtimeClasspath
}
defaultTasks 'runServer'

// task to run client
task(runClient, dependsOn: 'classes', type: JavaExec) {
  main = 'no.sysco.middleware.workshops.impl.Client'
  classpath = sourceSets.main.runtimeClasspath
}
defaultTasks 'runClient'